#Requires AutoHotkey v2.0
#SingleInstance force

未定义 := ""
当前活动窗口 := "A"
配置 := A_Args.Length ? A_Args[1] : "配置.ini"
函数参数分隔符 := ":"
参数列表分隔符 := "|"
字符串类型 := "String"
数组类型 := "Array"
分隔符 := A_Tab
一小会儿 := IniRead(配置, "缺省值", "一小会儿", 3000)
一小小会儿 := IniRead(配置, "缺省值", "一小小会儿", 1000)
需要步进 := IniRead(配置, "缺省值", "需要步进", false)
允许弹窗提示 :=  IniRead(配置, "缺省值", "允许弹窗提示", true)
流程 := IniRead(配置, "流程")

休息(时长)
{
    sleep 时长
}

弹窗(提示信息)
{
    if 允许弹窗提示
        msgbox 提示信息
}

步进提示(内容)
{
    if 需要步进
        弹窗 内容
}

校验类型(值, 类型)
{
    值类型 := Type(值)
    if not 值类型 == 类型
    {
        步进提示 值类型 "与" 类型 "数据类型不匹配"
        return false
    }
    return true
}

运行(程序)
{
    return Run(程序)
}

激活(窗口标题)
{
    WinActivate(窗口标题)
}

按图索骥(&横坐标, &竖坐标, 左上角x, 左上角y, 右下角x, 右下角y, 图片)
{
    横坐标 := 横坐标 := 0
    ret := ImageSearch(&横坐标, &竖坐标, 左上角x, 左上角y, 右下角x, 右下角y, 图片)
    if 0 == ret 
        弹窗 "没找到目标: " 左上角x "," 左上角y "," 右下角x "," 右下角y "," 图片
}

点击(目标坐标x, 目标坐标y)
{
    if 目标坐标x or 目标坐标y
        click 目标坐标x, 目标坐标y
    else
        弹窗 "不执行点击: " 目标坐标x ", " 目标坐标y
}

找到图片位置后点击(图片, 操作参数)
{
    偏移量x := 操作参数[1]
    偏移量y := 操作参数[2]
    宽度 := 高度 := 目标坐标x := 目标坐标y := 0
    左上角x := 左上角y := -200
    WinGetPos ,, &宽度, &高度, 当前活动窗口
    按图索骥 &目标坐标x, &目标坐标y, 左上角x, 左上角y, 宽度, 高度, "*10 " 图片
    if "Integer" == type(目标坐标x) and "Integer" == type(目标坐标y)
        点击 目标坐标x + 偏移量x, 目标坐标y + 偏移量y
    else
        弹窗 "目标坐标不存在: " 目标坐标x ", " 目标坐标y
}

对文件进行操作(文件, 操作, 操作参数)
{
    if FileExist(文件)
        %操作%(文件, 操作参数)
    else
        弹窗 文件 "文件不存在"
}

依次查找图片并点击(图库)
{
    if 校验类型(图库, 字符串类型)
    {
        图片 := 未定义
        相对偏移量 := [10, 5]
        loop parse, 图库, "`n", "`r"
        {
            图片 := A_LoopField
            步进提示 "对文件进行操作: " 图片
            对文件进行操作 图片, "找到图片位置后点击", 相对偏移量
            休息 一小小会儿
        }
    }
    if 校验类型(图库, 数组类型)
    {
        依次查找图片并点击(IniRead(配置, 图库[1]))
    }
}

输入(内容)
{
    sendText 内容
}

热键(热键列表)
{
    loop parse 热键列表 , "`n", "`r"
    {
        send A_Loopfield
        休息 一小小会儿
    }
}

找到图片位置后输入文本(图片, 操作参数)
{
    相对偏移量 := 操作参数[1]
    内容 := 操作参数[2]
    找到图片位置后点击 图片, 相对偏移量
    输入 内容
}

依次查找图片并输入文本(图文库)
{
    if 校验类型(图文库, 字符串类型)
    {
        图片 := 文字 := 未定义
        相对偏移量 := [50, 50]
        loop parse, 图文库, "`n", "`r"
        {
            图文 := StrSplit(A_LoopField, 分隔符)
            图片 := 图文[1]
            文字 := 图文[2]
            对文件进行操作 图片, "找到图片位置后输入文本", [相对偏移量, 文字]
            休息 一小小会儿
        }
    }
    if 校验类型(图文库, 数组类型)
    {
        依次查找图片并输入文本(IniRead(配置, 图文库[1]))
    }
}

执行(流程)
{
    if 校验类型(流程, 字符串类型)
    {
        loop parse 流程 , "`n", "`r"
        {
            条目 := StrSplit(A_LoopField, 函数参数分隔符)
            函数 := 条目[1]
            if not Instr(条目[2], 参数列表分隔符)
                %函数%(IniRead(配置, 条目[2]))
            else
                %函数%(StrSplit(条目[2], 参数列表分隔符))
        }
    }
}

主程序()
{
    执行(流程)
}

主程序